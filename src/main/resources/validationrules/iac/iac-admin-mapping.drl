package validationrules.iac;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.ActorIdType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.RoleType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.Classification;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.CaseWorkerAccessProfile;
import java.util.ArrayList;
import  uk.gov.hmcts.reform.orgrolemapping.util.JacksonUtils;
import java.util.HashMap
import java.util.Map
import com.fasterxml.jackson.databind.JsonNode;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.FeatureFlag;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.FeatureFlagEnum;
import function uk.gov.hmcts.reform.orgrolemapping.domain.service.RequestMappingService.logMsg;

/*
 * IAC Staff admin roles.
 */

rule "iac_hmcts-admin_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("3","4","5","6","7","8"), serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.ADMIN)
      .roleType(RoleType.ORGANISATION)
      .roleName("hmcts-admin")
      .grantType(GrantType.BASIC)
      .classification(Classification.PRIVATE)
      .readOnly(true)
      .attributes(attribute)
      .build());
      logMsg("Rule : iac_hmcts-admin_caseworker_v11");
end;

/*
 * "hearing-centre-admin" Org role mapping
 * Made obsolete in DTSAM-590 - rule will be disabled when IAC_WA_1_3 is enabled.
 * To be removed in DTSAM-594.
 */

rule "iac_hearing-centre-admin_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $f2: FeatureFlag(status == false && flagName == FeatureFlagEnum.IAC_WA_1_3.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("3","4","5"), serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("hearing_work, upper_tribunal, routine_work"));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.ADMIN)
      .roleType(RoleType.ORGANISATION)
      .roleName("hearing-centre-admin")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : iac_hearing-centre-admin_caseworker_v11");
end;

rule "iac_hearing-centre-admin_caseworker_v1_3"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_WA_1_3.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("3","4","5"), serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("hearing_work, upper_tribunal, routine_work, review_case"));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.ADMIN)
      .roleType(RoleType.ORGANISATION)
      .roleName("hearing-centre-admin")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : iac_hearing-centre-admin_caseworker_v1_3");
end;


rule "iac_national-business-centre_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("6","7","8"), serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("hearing_work, upper_tribunal, routine_work"));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.ADMIN)
      .roleType(RoleType.ORGANISATION)
      .roleName("national-business-centre")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : iac_national-business-centre_caseworker_v11");
end;


rule "iac_case_allocator_admin_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("3","4","5","6","7","8"), serviceCode == "BFA1", !suspended,
  caseAllocatorFlag == "Y")
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.ADMIN)
      .roleType(RoleType.ORGANISATION)
      .roleName("case-allocator")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : iac_case_allocator_admin_caseworker_v11");
end;

rule "iac_task-supervisor_admin_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("3","4","5","6","7","8"), serviceCode == "BFA1", !suspended,
  taskSupervisorFlag == "Y")
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.ADMIN)
      .roleType(RoleType.ORGANISATION)
      .roleName("task-supervisor")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : iac_task-supervisor_admin_caseworker_v11");
end;