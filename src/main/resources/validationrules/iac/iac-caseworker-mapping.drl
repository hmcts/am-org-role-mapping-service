package validationrules.iac;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.ActorIdType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.RoleType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.Classification;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.CaseWorkerAccessProfile;
import java.util.ArrayList;
import  uk.gov.hmcts.reform.orgrolemapping.util.JacksonUtils;
import java.util.HashMap
import java.util.Map
import com.fasterxml.jackson.databind.JsonNode;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.FeatureFlag;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.FeatureFlagEnum;
import function uk.gov.hmcts.reform.orgrolemapping.domain.service.RequestMappingService.logMsg;

/*
 * IAC case worker Org roles mapping rules for "senior-tribunal-caseworker", "tribunal-case-worker", "case-allocator"
 * and "task-supervisor" as version IAC_1_1. The STCW role would be deprecated later.
 * Made obsolete in DTSAM-1072 - disabled by iac_wa_1_5 flag.
 * To be removed in DTSAM-1086.
 */

rule "iac_senior_tribunal_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $f1: FeatureFlag(status == false && flagName == FeatureFlagEnum.IAC_WA_1_5.getValue())
  $up: CaseWorkerAccessProfile(roleId == "1", serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("hearing_work, routine_work, decision_making_work, applications"));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.LEGAL_OPERATIONS)
      .roleType(RoleType.ORGANISATION)
      .roleName("senior-tribunal-caseworker")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : iac_senior_tribunal_caseworker_v11");
end;

/*
 * IAC case worker Org roles mapping rules for "senior-tribunal-caseworker", "tribunal-case-worker", "case-allocator"
 *  and "task-supervisor" as version IAC_1_1. The STCW role would be deprecated later for 1.5.
 */
rule "v_1_5_iac_senior_tribunal_caseworker"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_WA_1_5.getValue())
  $up: CaseWorkerAccessProfile(roleId == "1", serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("hearing_work, routine_work, decision_making_work, applications, stf_24w_hearing_work, stf_24w_routine_work, stf_24w_decision_making_work, stf_24w_applications"));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.LEGAL_OPERATIONS)
      .roleType(RoleType.ORGANISATION)
      .roleName("senior-tribunal-caseworker")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : v_1_5_iac_senior_tribunal_caseworker");
end;

/*
 * IAC case worker Org roles mapping rules for "tribunal-caseworker"
 * Made obsolete in DTSAM-1072 - disabled by iac_wa_1_5 flag.
 * To be removed in DTSAM-1086.
 */
rule "iac_tribunal_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $f1: FeatureFlag(status == false && flagName == FeatureFlagEnum.IAC_WA_1_5.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("1","2"), serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("hearing_work, routine_work, decision_making_work, applications"));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.LEGAL_OPERATIONS)
      .roleType(RoleType.ORGANISATION)
      .roleName("tribunal-caseworker")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : iac_tribunal_caseworker_v11");
end;

/*
* IAC case worker Org roles mapping rules for "tribunal-caseworker"
* for 1.5.
*/
rule "v_1_5_iac_tribunal_caseworker"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_WA_1_5.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("1","2"), serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("hearing_work, routine_work, decision_making_work, applications, stf_24w_hearing_work, stf_24w_routine_work, stf_24w_decision_making_work, stf_24w_applications"));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.LEGAL_OPERATIONS)
      .roleType(RoleType.ORGANISATION)
      .roleName("tribunal-caseworker")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : v_1_5_iac_tribunal_caseworker");
end;

rule "iac_case_allocator_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("1","2"), serviceCode == "BFA1", !suspended,
                         caseAllocatorFlag == "Y")
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.LEGAL_OPERATIONS)
      .roleType(RoleType.ORGANISATION)
      .roleName("case-allocator")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());

      logMsg("Rule : iac_case_allocator_caseworker_v11");
end;

rule "iac_task_supervisor_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("1","2"), serviceCode == "BFA1", !suspended,
                         taskSupervisorFlag == "Y")
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("IA"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.LEGAL_OPERATIONS)
      .roleType(RoleType.ORGANISATION)
      .roleName("task-supervisor")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($up.getSkillCodes())
      .build());
      logMsg("Rule : iac_case_allocator_caseworker_v11");
end;



rule "iac_hmcts-legal-operations_caseworker_v11"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.IAC_1_1.getValue())
  $up: CaseWorkerAccessProfile(roleId in ("1","2"), serviceCode == "BFA1", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($up.getPrimaryLocationId()));
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($up.getId())
      .roleCategory(RoleCategory.LEGAL_OPERATIONS)
      .roleType(RoleType.ORGANISATION)
      .roleName("hmcts-legal-operations")
      .grantType(GrantType.BASIC)
      .classification(Classification.PRIVATE)
      .readOnly(true)
      .attributes(attribute)
      .build());
      logMsg("Rule : iac_hmcts-legal-operations_caseworker_v11");
end;