package validationrules.sscs;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.ActorIdType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.RoleType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.Classification;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.CaseWorkerAccessProfile;
import java.util.ArrayList;
import  uk.gov.hmcts.reform.orgrolemapping.util.JacksonUtils;
import java.util.HashMap
import java.util.Map
import com.fasterxml.jackson.databind.JsonNode;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.FeatureFlag;
import uk.gov.hmcts.reform.orgrolemapping.domain.model.enums.FeatureFlagEnum;
import function uk.gov.hmcts.reform.orgrolemapping.domain.service.RequestMappingService.logMsg;
import function uk.gov.hmcts.reform.orgrolemapping.helper.AssignmentRequestBuilder.cloneNewRoleAssignmentAndChangeRegion;

/*
 * SSCS ctsc "hmcts-ctsc" Org role mapping.
 */

rule "sscs_hmcts_ctsc_org_role"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $cap: CaseWorkerAccessProfile(roleId in ("9", "10"), serviceCode == "BBA3", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($cap.getId())
      .roleCategory(RoleCategory.CTSC)
      .roleType(RoleType.ORGANISATION)
      .roleName("hmcts-ctsc")
      .grantType(GrantType.BASIC)
      .classification(Classification.PRIVATE)
      .readOnly(true)
      .attributes(attribute)
      .build());
      logMsg("Rule : sscs_hmcts_ctsc_org_role");
end;

/*
 * SSCS ctsc "ctsc" Org role mapping.
 */

rule "sscs_ctsc_org_role"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $cap: CaseWorkerAccessProfile(roleId in ("9", "10"), serviceCode == "BBA3", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("SSCS"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($cap.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("pre_hearing,post_hearing,decision_making_work,applications,hearing_work,routine_work,priority"));
   attribute.put("region", JacksonUtils.convertObjectIntoJsonNode($cap.getRegionId()));

  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($cap.getId())
      .roleCategory(RoleCategory.CTSC)
      .roleType(RoleType.ORGANISATION)
      .roleName("ctsc")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($cap.getSkillCodes())
      .build());
      logMsg("Rule : sscs_ctsc_org_role");
end;

/*
 * SSCS ctsc "ctsc-team-leader" Org role mapping.
 */

rule "sscs_ctsc_team_leader_org_role"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $cap: CaseWorkerAccessProfile(roleId == "9", serviceCode == "BBA3", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("SSCS"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($cap.getPrimaryLocationId()));
   attribute.put("region", JacksonUtils.convertObjectIntoJsonNode($cap.getRegionId()));

  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($cap.getId())
      .roleCategory(RoleCategory.CTSC)
      .roleType(RoleType.ORGANISATION)
      .roleName("ctsc-team-leader")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .authorisations($cap.getSkillCodes())
      .build());
      logMsg("Rule : sscs_ctsc_team_leader_org_role");
end;

/*
 * SSCS ctsc "task-supervisor" Org role mapping.
 */

rule "sscs_ctsc_task_supervisor_org_role"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $cap: CaseWorkerAccessProfile(roleId in ("9", "10"), serviceCode == "BBA3", !suspended,
                            taskSupervisorFlag == "Y")
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("SSCS"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($cap.getPrimaryLocationId()));
   attribute.put("region", JacksonUtils.convertObjectIntoJsonNode($cap.getRegionId()));

  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($cap.getId())
      .roleCategory(RoleCategory.CTSC)
      .roleType(RoleType.ORGANISATION)
      .roleName("task-supervisor")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .build());
      logMsg("Rule : sscs_ctsc_task_supervisor_org_role");
end;

/*
 * SSCS ctsc "case-allocator" Org role mapping.
 */

rule "sscs_ctsc_case_allocator_org_role"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $cap: CaseWorkerAccessProfile(roleId in ("9", "10"), serviceCode == "BBA3", !suspended,
                            caseAllocatorFlag == "Y")
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("SSCS"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($cap.getPrimaryLocationId()));
   attribute.put("region", JacksonUtils.convertObjectIntoJsonNode($cap.getRegionId()));

  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($cap.getId())
      .roleCategory(RoleCategory.CTSC)
      .roleType(RoleType.ORGANISATION)
      .roleName("case-allocator")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .build());
      logMsg("Rule : sscs_ctsc_case_allocator_org_role");
end;

/*
 * SSCS ctsc "specific-access-approver-ctsc" Org role mapping.
 */

rule "sscs_specific_access_approver_ctsc_org_role"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $cap: CaseWorkerAccessProfile(roleId == "9", serviceCode == "BBA3", !suspended)
then
   Map<String,JsonNode> attribute = new HashMap<>();
   attribute.put("jurisdiction", JacksonUtils.convertObjectIntoJsonNode("SSCS"));
   attribute.put("primaryLocation", JacksonUtils.convertObjectIntoJsonNode($cap.getPrimaryLocationId()));
   attribute.put("workTypes", JacksonUtils.convertObjectIntoJsonNode("access_requests"));
   attribute.put("region", JacksonUtils.convertObjectIntoJsonNode($cap.getRegionId()));

  insert(
      RoleAssignment.builder()
      .actorIdType(ActorIdType.IDAM)
      .actorId($cap.getId())
      .roleCategory(RoleCategory.CTSC)
      .roleType(RoleType.ORGANISATION)
      .roleName("specific-access-approver-ctsc")
      .grantType(GrantType.STANDARD)
      .classification(Classification.PUBLIC)
      .readOnly(false)
      .attributes(attribute)
      .build());
      logMsg("Rule : sscs_specific_access_approver_ctsc_org_role");
end;


/*
 * SSCS "multi-region" Org role mapping.
 */

rule "sscs_multi_region_dependency_org_role__ctsc__region_1"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $ra: RoleAssignment(roleCategory == RoleCategory.CTSC,
                      attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                      attributes["region"] != null && attributes["region"].asText() in ("2","3","4","5","6","7"))
  not RoleAssignment(actorId == $ra.getActorId(),
                     roleName == $ra.getRoleName(),
                     roleCategory == RoleCategory.CTSC,
                     attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                     attributes["region"] != null && attributes["region"].asText() == "1")
then
   insert(cloneNewRoleAssignmentAndChangeRegion($ra, "1"));
   logMsg("Rule : sscs_multi_region_dependency_org_role__ctsc__region_1");
end;

rule "sscs_multi_region_dependency_org_role__ctsc__region_2"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $ra: RoleAssignment(roleCategory == RoleCategory.CTSC,
                      attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                      attributes["region"] != null && attributes["region"].asText() in ("1","3","4","5","6","7"))
  not RoleAssignment(actorId == $ra.getActorId(),
                     roleName == $ra.getRoleName(),
                     roleCategory == RoleCategory.CTSC,
                     attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                     attributes["region"] != null && attributes["region"].asText() ==  "2")
then
   insert(cloneNewRoleAssignmentAndChangeRegion($ra, "2"));
   logMsg("Rule : sscs_multi_region_dependency_org_role__ctsc__region_2");
end;

rule "sscs_multi_region_dependency_org_role__ctsc__region_3"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $ra: RoleAssignment(roleCategory == RoleCategory.CTSC,
                      attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                      attributes["region"] != null && attributes["region"].asText() in ("1","2","4","5","6","7"))
  not RoleAssignment(actorId == $ra.getActorId(),
                     roleName == $ra.getRoleName(),
                     roleCategory == RoleCategory.CTSC,
                     attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                     attributes["region"] != null && attributes["region"].asText() == "3")
then
   insert(cloneNewRoleAssignmentAndChangeRegion($ra, "3"));
   logMsg("Rule : sscs_multi_region_dependency_org_role__ctsc__region_3");
end;

rule "sscs_multi_region_dependency_org_role__ctsc__region_4"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $ra: RoleAssignment(roleCategory == RoleCategory.CTSC,
                      attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                      attributes["region"] != null && attributes["region"].asText() in ("1","2","3","5","6","7"))
  not RoleAssignment(actorId == $ra.getActorId(),
                     roleName == $ra.getRoleName(),
                     roleCategory == RoleCategory.CTSC,
                     attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                     attributes["region"] != null && attributes["region"].asText() == "4")
then
   insert(cloneNewRoleAssignmentAndChangeRegion($ra, "4"));
   logMsg("Rule : sscs_multi_region_dependency_org_role__ctsc__region_4");
end;

rule "sscs_multi_region_dependency_org_role__ctsc__region_5"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $ra: RoleAssignment(roleCategory == RoleCategory.CTSC,
                      attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                      attributes["region"] != null && attributes["region"].asText() in ("1","2","3","4","6","7"))
  not RoleAssignment(actorId == $ra.getActorId(),
                     roleName == $ra.getRoleName(),
                     roleCategory == RoleCategory.CTSC,
                     attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                     attributes["region"] != null && attributes["region"].asText() == "5")
then
   insert(cloneNewRoleAssignmentAndChangeRegion($ra, "5"));
   logMsg("Rule : sscs_multi_region_dependency_org_role__ctsc__region_5");
end;

rule "sscs_multi_region_dependency_org_role__ctsc__region_6"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $ra: RoleAssignment(roleCategory == RoleCategory.CTSC,
                      attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                      attributes["region"] != null && attributes["region"].asText() in ("1","2","3","4","5","7"))
  not RoleAssignment(actorId == $ra.getActorId(),
                     roleName == $ra.getRoleName(),
                     roleCategory == RoleCategory.CTSC,
                     attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                     attributes["region"] != null && attributes["region"].asText() == "6")
then
   insert(cloneNewRoleAssignmentAndChangeRegion($ra, "6"));
   logMsg("Rule : sscs_multi_region_dependency_org_role__ctsc__region_6");
end;

rule "sscs_multi_region_dependency_org_role__ctsc__region_7"
when
  $f:  FeatureFlag(status && flagName == FeatureFlagEnum.SSCS_WA_1_0.getValue())
  $ra: RoleAssignment(roleCategory == RoleCategory.CTSC,
                      attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                      attributes["region"] != null && attributes["region"].asText() in ("1","2","3","4","5","6"))
  not RoleAssignment(actorId == $ra.getActorId(),
                     roleName == $ra.getRoleName(),
                     roleCategory == RoleCategory.CTSC,
                     attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() in ("SSCS"),
                     attributes["region"] != null && attributes["region"].asText() == "7")
then
   insert(cloneNewRoleAssignmentAndChangeRegion($ra, "7"));
   logMsg("Rule : sscs_multi_region_dependency_org_role__ctsc__region_7");
end;
